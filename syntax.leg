%{
#include <stdio.h>
#include <signal.h>
#include <time.h>
#include <stdarg.h>
#include "bstrlib.h"
#include "list.h"

#define YY_noDEBUG 1
#define KBYTE 1024
#define MBYTE KBYTE * 1024
#define STR_BUFFER_LEN 1024

#define IMAGE_FRAME 1
#define IMAGE_THUMB ( 1 << 1 )

#define TOC_NOTOC ( 1 << 1 )
#define TOC_FORCETOC ( 1 << 2 )
#define TOC_RELOC ( 1 << 3 )


int current_header_level = 0;
int current_bullet_list_level;
int current_numbered_list_level;

int start_of_line = 1;
char protocol[5];

int image_attributes = 0;
bstring image_url;
bstring image_variables;

int toc_attributes = 0;

int in_tag = 0;
bstring tag_content;
unsigned int tag_content_size = 2 * MBYTE;

bstring output_buffer;
bstring toc_buffer;

struct list list;

void bprintf(const char *fmt, ...) {
  int ret;
  bvformata(ret, output_buffer, fmt, fmt);
}

int list_li_print(struct node *item) {
  if(!item) {
    return 0;
  }
  bformata(toc_buffer, "<li><a href=\"#%s\">%s</a>\n", bdata(item->name), bdata(item->content));
  return 1;
}

void handle_toc() {
  if(toc_attributes & TOC_NOTOC) {
    return;
  }

  if((list.size > 3) || (toc_attributes & TOC_FORCETOC)) {
    bformata(toc_buffer, "%s", "<ol>");
    list_iterate(list.head->next, list_li_print);
    bformata(toc_buffer, "%s", "</ol>");

    if(toc_attributes & TOC_RELOC) {
      bstring find = bfromcstr("__TOC__");
      if(bfindreplace(output_buffer, find, toc_buffer, 0) == BSTR_ERR) {
        printf("Error inserting toc_buffer into output_buffer\n");
	  }
      bdestroy(find);
      return;
    }

    if(binsert(output_buffer, 0, toc_buffer, ' ') != BSTR_OK) {
      printf("Error prepending toc_buffer to output_buffer\n");
    }
  }
}

void remove_parentheticals(bstring str) {
  char *temp = bstr2cstr(str, '\0');

  int i, index = 0;
  int in_tag = 0;
  for(i = 0; i < blength(str); i++) {
    if((bdata(str))[i] == '(') {
      in_tag++;
      continue;
    } else if((bdata(str))[i] == ')') {
      in_tag--;
      continue;
    }
    if(in_tag < 1) temp[index++] = (bdata(str))[i];
  }
  temp[index] = '\0';
  bdestroy(str);
  str = bfromcstr(temp);
  bcstrfree(temp);
}

void strip_tags(bstring str) {
  char *temp = bstr2cstr(str, '\0');

  int i, index = 0;
  int in_tag = 0;
  for(i = 0; i < blength(str); i++) {
    if((bdata(str))[i] == '<') {
      in_tag++;
      continue;
    } else if((bdata(str))[i] == '>') {
      in_tag--;
      continue;
    }
    if(in_tag < 1) temp[index++] = (bdata(str))[i];
  }
  temp[index] = '\0';
  bdestroy(str);
  str = bfromcstr(temp);
  bcstrfree(temp);
}

void urlencode(bstring b) {
  char *p;
  int c, e;
  bstring target = bfromcstr("");

  int whitelist[] = {
    /* reserved characters */
    [36] '$',
    [38] '&',
    [43] '+',
    [44] ',',
	[46] '.',
    [47] '/',
    [58] ':',
    [59] ';',
    [61] '=',
    [63] '?',
    [64] '@',
	[95] '_'
  };

  p = bdata(b);
  while ((c = *p++)) { // Double parens to make gcc shut up
    /* [0-9A-Za-z] */
    if (('0' <= c && c <= '9') ||
        ('A' <= c && c <= 'Z') ||
        ('a' <= c && c <= 'z')) {
      bformata(target, "%c", c);
      continue;
    }
    e = whitelist[c];
    if (e) {
      bformata(target, "%c", c);
      continue;
    }
    bformata(target, "%%%02x", c);
  }
  bassign(b, target);
}

void strip_html_markup(bstring str) {
  strip_tags(str);
  brtrimws(str);
  bstring find = bfromcstr(" ");
  bstring replace = bfromcstr("_");
  bfindreplace(str, find, replace, 0);
  bdestroy(find);
  bdestroy(replace);
}

void open_tag(char *tag, char *args) {
  if(args) {
    bprintf("<%s %s>", tag, args);
  } else {
    bprintf("<%s>", tag);
  }

  in_tag = 1;
  if(tag_content) {
    btrunc(tag_content,0 );
  }
}

void close_tag(char *tag) {
  bprintf("</%s>", tag);
  in_tag = 0;
}

void append_to_tag_content(char *fmt, ...) {
  int ret;

  if(!in_tag) {
    bvformata(ret, output_buffer, fmt, fmt);
    return;
  }

  bvformata(ret, tag_content, fmt, fmt);
}

%}

# High-level
page =  nowiki | pre_block | wikitext_line | wikitext
wikitext_line = paragraph | horizontal_line 
wikitext = table_of_contents | template | heading | indentation | list | formatting | image | link | table | anything_else | eol

# Generally useful rules
eol = '\n' { start_of_line = 1; }
eof = !.
space = [\t ]

# Headings
heading = heading_marker {
    char tag[4];
    sprintf(tag, "h%d", current_header_level);
    open_tag(tag, NULL);
  } space* heading_text heading_marker space* ( eol | eof ) 
  {
    brtrimws(tag_content);
    bstring tmp = bstrcpy(tag_content);
    strip_html_markup(tmp);
    urlencode(tmp);
    bprintf("<span class=\"editsection\">[<a href=\"edit\">edit</a>]</span><span class=\"mw-headline\" id=\"%s\">", bdata(tmp));
    bdestroy(tmp);

    bprintf("%s</span>", bdata(tag_content));
    char tag[4];
    sprintf(tag, "h%d", current_header_level);
    close_tag(tag);

	struct node *current = list_get_new_tail(&list);

    tmp = bstrcpy(tag_content);
    strip_html_markup(tmp);
	urlencode(tmp);
    bprintf("<a name=\"%s\" />", bdata(tmp));
	bassign(current->name, tmp);

	bstring human_name = bstrcpy(tag_content);
	strip_tags(human_name);
	brtrimws(human_name);
	bassign(current->content, human_name);
	current->level = current_header_level;

    bdestroy(tmp);
	bdestroy(human_name);

    btrunc(tag_content, 0);
    current_header_level = 0;
  }
heading_text = < ( !heading_marker ( formatting | link | anything_else ) )* > 
heading_marker = < '=' '='+ > { if(current_header_level == 0) current_header_level = (yyleng > 5) ? 5 : yyleng; }

# Paragraph
paragraph = wikitext eol eol { bprintf("</p><p>"); }
horizontal_line = "----" ( eol | eof )  { bprintf("<hr/>"); }

# Indentation
indentation = &{ start_of_line } ':' { bprintf("&nbsp;&nbsp;"); } wikitext

# Lists
list = bullet_list | numbered_list

# Bullet list
bullet_list = { bprintf("<ul>"); } bullet_list_entry+ { 
  while((current_bullet_list_level > 0) && current_bullet_list_level--) { 
    bprintf("</ul>"); 
  }
}
bullet_list_entry = bullet space* ( !eol wikitext )* ( eol | eof ) { bprintf("</li>"); }
bullet = < '*'+ > {
  start_of_line = 0;
  if(current_bullet_list_level < yyleng && current_bullet_list_level != 0) {
    bprintf("<ul>");
  } else if(current_bullet_list_level > yyleng) {
    bprintf("</ul>");
  }
  current_bullet_list_level = yyleng; 
  bprintf("<li>"); 
}

# Numbered list
numbered_list = { bprintf("<ol>"); } numbered_list_entry+ { while(current_numbered_list_level--) { bprintf("</ol>"); } }
numbered_list_entry = numbered space* ( !eol wikitext )* eol? { bprintf("</li>"); }
numbered = < '#'+ > {
  start_of_line = 0;
  if(current_numbered_list_level < yyleng && current_numbered_list_level != 0) {
    bprintf("<ol>");
  } else if(current_numbered_list_level > yyleng) {
    bprintf("</ol>");
  }
  current_numbered_list_level = yyleng; 
  bprintf("<li>"); 
}

# Formatting
formatting = bold_italic | bold | italic
bold_italic = "'''''" {append_to_tag_content("<b><i>");}( !"'''''" wikitext )* "'''''" {append_to_tag_content("</i></b>");}
bold = "'''" {append_to_tag_content("<b>");}( !"'''" wikitext )* "'''" {append_to_tag_content("</b>");}
italic = "''" {append_to_tag_content("<i>");}( !"''" wikitext )* "''" {append_to_tag_content("</i>");}

# pre-formatted blocks
pre_block = &{ start_of_line } { bprintf("<pre>"); } pre_line+ { bprintf("</pre>"); }
pre_line = ' ' < ( !eol . )* ( eol | eof ) > { bprintf("%s", yytext); }
nowiki = '<nowiki>' ( !'</nowiki>' anything_else )* '</nowiki>'

# Links
link = renamed_link | simple_link | external_link { start_of_line = 0; }
renamed_link = '[[' < ( !'|' !']]' . )* > { 
    bstring path = bfromcstr(yytext);
    strip_html_markup(path);
	urlencode(path);
    append_to_tag_content("<a href=\"/%s\">", bdata(path));
    bdestroy(path);
  } '|' < ( !']]' . )* > { 
    append_to_tag_content("%s</a>", yytext); 
    start_of_line = 0; // prevent <pre> block from starting due to the next space
  } ']]'
simple_link = '[[' ':'? < ( !']]' . )* > {
    bstring path = bfromcstr(yytext);
    strip_html_markup(path);
	urlencode(path);
    bstring name = bfromcstr(yytext);
    remove_parentheticals(name);
    brtrimws(name);
    append_to_tag_content("<a href=\"/%s\">%s", bdata(path), bdata(name));
    bdestroy(path);
    bdestroy(name);
  } ']]' < ( !space !( '=' '='+  ) !'[' !eol !eof . )* > { 
    append_to_tag_content("%s</a>", yytext); 
    start_of_line = 0; // prevent <pre> block from starting due to the next space 
  }
external_link = named_link | unnamed_link | url
named_link = '[' { append_to_tag_content("<a href=\""); } link_url { append_to_tag_content("\">");} ' '+ link_name + space* ']' {append_to_tag_content("</a>"); }
link_url = < ( !' ' . )+ >  { append_to_tag_content(yytext); }
link_name = < ( !']' . )+ > { append_to_tag_content(yytext); }
unnamed_link = '[' < ( !']' . )* > ']' { append_to_tag_content("<a href=\"%s\">%s</a>", yytext, yytext);}
url = < ( 'http' | 'https' | 'ftp' ) > { strcpy(protocol, yytext); } '://' < ( !eol !' ' . )* > { append_to_tag_content("<a href=\"%s://%s\">%s://%s</a>", protocol, yytext, protocol, yytext); }

# Images
image = open_image { btrunc(image_variables, 0); } file_indicator arguments? close_image {
  if(image_attributes & IMAGE_FRAME) {
    bprintf("<div class=\"thumb tright\"><div class=\"thumbinner\">");
  }

  if(image_attributes & IMAGE_THUMB) {
    bprintf("<a href=\"File:%s\" class=\"image\"><img src=\"%s\" width=\"220\" height=\"30\" class=\"thumbimage\"/></a>", bdata(image_url), bdata(image_url));
  } else if(image_attributes & IMAGE_FRAME) {
    bprintf("<a href=\"File:%s\" class=\"image\"><img src=\"%s\" class=\"thumbimage\"/></a>", bdata(image_url), bdata(image_url));
  } else {
    bprintf("<a href=\"File:%s\" class=\"image\"><img src=\"%s\" %s/></a>", bdata(image_url), bdata(image_url), bdata(image_variables));
  }

  if(image_attributes & IMAGE_FRAME) {
    bprintf("<div class=\"thumbcaption\"></div></div>");
  }
} 
open_image = '[[' space*
close_image = space* ']]'
file_indicator = ( 'File:' | 'Image:' ) < ( !'|' !close_image . )* > { bassignformat(image_url, "%s", yytext); }
arguments = '|' variable value? arguments?
variable = special_image_variables | generic_variable
special_image_variables = image_type | float
image_type = frame | thumb
frame = 'frame' { image_attributes |= IMAGE_FRAME; }
thumb = ( 'thumbnail' | 'thumb' ) { image_attributes |= IMAGE_THUMB; }
float = < ( 'left' | 'right' | 'center' ) > { 
  bformata(image_variables, " class=\"float%s\"", yytext);
} | 'none'
generic_variable = < ( !'|' !'=' !close_image . )* > { 
  bformata(image_variables, "%s", yytext);
}
value = '=' < ( !'|' !close_image . )* > { 
  bformata(image_variables, "=\"%s\"", yytext);
}

# Tables
table = '{|' 
  { bprintf("<table>"); }
  eol table_body '|}' ( eol | eof )
  { bprintf("</table>"); }
table_body = table_caption? table_header? table_row+
table_caption = '|+' ' '+ < ( !eol . )* > eol { bprintf("<caption>%s</caption>", yytext); }
table_row = '|-'
  { bprintf("<tr>"); }
  space* eol table_heading* table_cell*
  { bprintf("</tr>"); }
table_cell = '|'?
  { bprintf("<td>"); }
  ' '+ < ( !'||' !eol . )* > ( eol | '||' )
  { bprintf("%s</td>", yytext); }
table_header = { bprintf("<tr>"); } table_heading+ { bprintf("</tr>"); }
table_heading = { bprintf("<th"); }'!' ' '+ < ( !' |' . )* > { bprintf(" %s>", yytext); } ' |' space* < ( !eol . )* > eol {bprintf("%s</th>", yytext); }

# Templates
template = '{{' space* ( !'}}' . )* '}}' { start_of_line = 0; }

# Table of Contents
table_of_contents = ( notoc | toc | forcetoc )
notoc = '__NOTOC__' { toc_attributes |= TOC_NOTOC; }
forcetoc = '__FORCETOC__' { toc_attributes |= TOC_FORCETOC; }
toc = '__TOC__' { toc_attributes |= TOC_RELOC; bprintf("__TOC__"); // pass-through for later replacement }

# Other shit
anything_else = < !eol . > { start_of_line = 0; append_to_tag_content(yytext); }


%%

int main() {
  list_init(&list);

  tag_content = bfromcstr("");
  ballocmin(tag_content, tag_content_size);

  image_variables = bfromcstr("");
  image_url = bfromcstr("");

  output_buffer = bfromcstr("");
  ballocmin(output_buffer, 1 * MBYTE);

  toc_buffer = bfromcstr("");

  bprintf("<p>");
  while(yyparse()) {}
  bprintf("</p>");
  bdestroy(tag_content);
  bdestroy(image_variables);
  bdestroy(image_url);

  handle_toc();
  printf("%s", bdata(output_buffer));
  bdestroy(output_buffer);
  bdestroy(toc_buffer);
  list_free(&list);

  return 0;

  /* Make gcc warnings about unused yyunmatched go away... */
  (void)yyunmatched;
}
